// vim: ft=rust
use core::{RpNumber, Loc, RpModifier, RpPackage, Object, VersionReq};
use std::rc::Rc;
use ast::*;
use lexer::{self, Token};
use super::utils::*;

grammar<'input, 'object>(input_object: &'object Rc<Box<Object>>);

pub File: File<'input> = {
    <comment:"//!"?>
    <uses:Loc<Use>*>
    <options:Loc<OptionDecl>*>
    <decls:Loc<Decl>*> => File {
        comment: comment.unwrap_or_else(Vec::new),
        options: options,
        uses: uses,
        decls: decls,
    },
};

Use: UseDecl<'input> =
    "use" <package:Loc<Package>>
        <version_req:Loc<"string">?>
        <alias:Loc<UseAlias>?>
    ";" =>
{
    UseDecl {
        package: package,
        version_req: version_req,
        alias: alias,
    }
};

UseAlias: &'input str = "as" <value:ident> => value;

Package: RpPackage =
    <parts:OneOrMore<".", ident>> =>
{
    RpPackage::new(parts.into_iter().map(ToOwned::to_owned).collect())
};

Decl: Decl<'input> = {
    <en:Enum> => Decl::Enum(en),
    <interface:Interface> => Decl::Interface(interface),
    <ty:Type> => Decl::Type(ty),
    <tuple:Tuple> => Decl::Tuple(tuple),
    <service:Service> => Decl::Service(service),
};

Enum: Loc<EnumBody<'input>> =
    <comment:"///"?>
    <s:@L>
    "enum" <name:TypeIdent> <ty:("as" Loc<TypeSpec>)?> "{"
        <variants:EnumVariant*>
        <members:Member*>
    "}"
    <e:@R> =>
{
    Loc::new(EnumBody {
        name: name,
        comment: comment.unwrap_or_else(Vec::new),
        ty: ty.map(|ty| ty.1),
        variants: variants,
        members: members,
    }, (input_object.clone(), s, e))
};

Interface: Loc<InterfaceBody<'input>> = {
    <comment:"///"?>
    <s:@L>
    "interface" <name:TypeIdent> "{"
      <members:Member*>
      <sub_types:SubType*>
    "}"
    <e:@R> =>
    {
        Loc::new(InterfaceBody {
            name: name,
            comment: comment.unwrap_or_else(Vec::new),
            members: members,
            sub_types: sub_types,
        }, (input_object.clone(), s, e))
    },
};

Type: Loc<TypeBody<'input>> =
    <comment:"///"?>
    <s:@L>
    "type" <name:TypeIdent> "{"
        <members:Member*>
    "}"
    <e:@R> =>
{
    Loc::new(TypeBody {
        name: name,
        comment: comment.unwrap_or_else(Vec::new),
        members: members,
    }, (input_object.clone(), s, e))
};

Tuple: Loc<TupleBody<'input>> =
    <comment:"///"?>
    <s:@L>
    "tuple" <name:TypeIdent> "{"
        <members:Member*>
    "}"
    <e:@R> =>
{
    Loc::new(TupleBody {
        name: name,
        comment: comment.unwrap_or_else(Vec::new),
        members: members,
    }, (input_object.clone(), s, e))
};

Service: Loc<ServiceBody<'input>> =
    <comment:"///"?>
    <s:@L>
    "service" <name:TypeIdent> "{"
        <members:ServiceMember*>
    "}"
    <e:@R> =>
{
    Loc::new(ServiceBody {
        name: name,
        comment: comment.unwrap_or_else(Vec::new),
        members: members,
    }, (input_object.clone(), s, e))
};

pub ServiceMember: ServiceMember<'input> = {
    <endpoint:Endpoint> => {
        ServiceMember::Endpoint(endpoint)
    },

    <option:Loc<OptionDecl>> => {
        ServiceMember::Option(option)
    },

    <decl:Loc<Decl>> => {
        ServiceMember::InnerDecl(decl)
    },
};

Endpoint: Loc<Endpoint<'input>> = {
    <comment:"///"?>
    <attributes:Loc<Attribute>*>
    <s:@L>
    <id:Loc<ident>> "(" <request:Loc<Channel>?> ")" <response:("->" Loc<Channel>)?>
    <alias:FieldAlias?>
    <e:@R>
    ";" => {
        Loc::new(Endpoint {
            id: id,
            comment: comment.unwrap_or_else(Vec::new),
            attributes: attributes,
            alias: alias,
            options: vec![],
            request: request,
            response: response.map(|r| r.1),
        }, (input_object.clone(), s, e))
    },

    <comment:"///"?>
    <attributes:Loc<Attribute>*>
    <s:@L>
    <id:Loc<ident>> "(" <request:Loc<Channel>?> ")" <response:("->" Loc<Channel>)?>
    <alias:FieldAlias?> "{"
        <options:Loc<OptionDecl>*>
    "}"
    <e:@R> => {
        Loc::new(Endpoint {
            id: id,
            comment: comment.unwrap_or_else(Vec::new),
            attributes: attributes,
            alias: alias,
            options: options,
            request: request,
            response: response.map(|r| r.1),
        }, (input_object.clone(), s, e))
    },
};

Channel: Channel = {
    <stream:stream?> <ty:TypeSpec> => {
        if stream.is_some() {
            Channel::Streaming {
                ty: ty
            }
        } else {
            Channel::Unary {
                ty: ty
            }
        }
    }
};

FieldAlias: String = {
    "as" <value:ident> => value.to_owned(),
    "as" <value:"string"> => value,
};

EnumVariant: Loc<EnumVariant<'input>> =
    <comment:"///"?>
    <s:@L>
    <name:Loc<TypeIdent>> <argument:("as" Loc<Value>)?> ";"
    <e:@R> =>
{
    Loc::new(EnumVariant {
        name: name,
        comment: comment.unwrap_or_else(Vec::new),
        argument: argument.map(|a| a.1),
    }, (input_object.clone(), s, e))
};

Modifier: RpModifier =
    <modifier:"?"?> =>
{
    modifier.map(|_| RpModifier::Optional).unwrap_or(RpModifier::Required)
};

pub Member: Loc<Member<'input>> = {
    <comment:"///"?>
    <s:@L> <name:ident> <modifier:Modifier> ":" <ty:TypeSpec> <alias:FieldAlias?> ";" <e:@R> =>
    {
        let field = Field {
            modifier: modifier,
            name: name,
            comment: comment.unwrap_or_else(Vec::new),
            ty: ty,
            field_as: alias,
        };

        let member = Member::Field(field);
        Loc::new(member, (input_object.clone(), s, e))
    },

    <s:@L> <option:OptionDecl> <e:@R> =>
    {
        let member = Member::Option(option);
        Loc::new(member, (input_object.clone(), s, e))
    },

    <s:@L> <context:ident> "{{" <content:code> "}}" <e:@R> =>
    {
        let content = strip_code_block(content);
        let member = Member::Code(context, content);
        Loc::new(member, (input_object.clone(), s, e))
    },

    <s:@L> <decl:Decl> <e:@R> =>
    {
        let member = Member::InnerDecl(decl);
        Loc::new(member, (input_object.clone(), s, e))
    },
};

OptionDecl: OptionDecl<'input> =
    option <name:ident> "=" <value:Loc<Value>> ";" =>
{
    OptionDecl {
        name: name,
        value: value,
    }
};

SubType: Loc<SubType<'input>> = {
    <comment:"///"?>
    <s:@L> 
    <name:Loc<TypeIdent>> <alias:("as" Loc<Value>)?> "{"
        <members:Member*>
    "}"
    <e:@R> =>
    {
        let sub_type = SubType {
            name: name,
            comment: comment.unwrap_or_else(Vec::new),
            members: members,
            alias: alias.map(|alias| alias.1),
        };

        Loc::new(sub_type, (input_object.clone(), s, e))
    },

    <comment:"///"?>
    <s:@L>
    <name:Loc<TypeIdent>> <alias:("as" Loc<Value>)?> ";"
    <e:@R> =>
    {
        let sub_type = SubType {
            name: name,
            comment: comment.unwrap_or_else(Vec::new),
            members: Vec::new(),
            alias: alias.map(|alias| alias.1),
        };

        Loc::new(sub_type, (input_object.clone(), s, e))
    },
};

pub Value: Value<'input> = {
    "[" <values:ZeroOrMore<",", Loc<Value>>> "]" => Value::Array(values),
    <string:"string"> => Value::String(string),
    <number:number> => Value::Number(number),
    <true> => Value::Boolean(true),
    <false> => Value::Boolean(false),
    <ident:ident> => Value::Identifier(ident),
};

pub TypeSpec: Type = {
    any => Type::Any,
    float => Type::Float,
    double => Type::Double,
    i32 => Type::Signed { size: 32 },
    i64 => Type::Signed { size: 64 },
    u32 => Type::Unsigned { size: 32 },
    u64 => Type::Unsigned { size: 64 },
    boolean => Type::Boolean,
    string => Type::String,
    datetime => Type::DateTime,
    bytes => Type::Bytes,
    <name:Name> => Type::Name{
        name: name
    },
    "[" <inner:TypeSpec> "]" => Type::Array {
        inner: Box::new(inner)
    },
    "{" <key:TypeSpec> ":" <value:TypeSpec> "}" => Type::Map{
        key: Box::new(key),
        value: Box::new(value)
    },
};

Name: Name = {
    <prefix:(ident "::")?> <parts:OneOrMore<"::", TypeIdent>> => Name::Absolute {
        prefix: prefix.map(|p| p.0.to_owned()),
        parts: parts.into_iter().map(ToOwned::to_owned).collect(),
    },

    "::" <parts:OneOrMore<"::", TypeIdent>> => Name::Relative {
        parts: parts.into_iter().map(ToOwned::to_owned).collect(),
    },
};

Attribute: Attribute<'input> = {
    "#" "[" <ident:Loc<ident>>  "]" => {
        Attribute::Word(ident)
    },
    "#" "[" <ident:Loc<ident>> "(" <name_value:OneOrMore<",", AttributeItem>> ")" "]" => {
        Attribute::List(ident, name_value)
    },
};

AttributeItem: AttributeItem<'input> = {
    <ident:Loc<ident>> "=" <value:Loc<Value>> => AttributeItem::NameValue {
        name: ident,
        value: value,
    },

    <ident:Loc<ident>> => {
        AttributeItem::Word(ident)
    },
};

/// Zero or more matching, separated by a token.
ZeroOrMore<Sep, T>: Vec<T> = {
    <value:OneOrMore<Sep, T>?> => {
        value.unwrap_or_else(|| vec![])
    }
};

/// One or more matching, separated by a token.
OneOrMore<Sep, T>: Vec<T> = {
     <values:(<T> Sep)*> <last:T> => {
        let mut values = values;
        values.push(last);
        values
    }
};

Loc<Inner>: Loc<Inner> = <s:@L> <inner:Inner> <e:@R> => {
    Loc::new(inner, (input_object.clone(), s, e))
};

extern {
    type Location = usize;
    type Error = lexer::Error;

    enum Token<'input> {
        TypeIdent => Token::TypeIdentifier(<&'input str>),
        ident => Token::Identifier(<&'input str>),
        number => Token::Number(<RpNumber>),
        "use" => Token::UseKeyword,
        "as" => Token::AsKeyword,
        "enum" => Token::EnumKeyword,
        "type" => Token::TypeKeyword,
        "interface" => Token::InterfaceKeyword,
        "tuple" => Token::TupleKeyword,
        "service" => Token::ServiceKeyword,
        "(" => Token::LeftParen,
        ")" => Token::RightParen,
        "{" => Token::LeftCurly,
        "}" => Token::RightCurly,
        "{{" => Token::CodeOpen,
        "}}" => Token::CodeClose,
        code => Token::CodeContent(<&'input str>),
        "string" => Token::String(<String>),
        "[" => Token::LeftBracket,
        "]" => Token::RightBracket,
        ";" => Token::SemiColon,
        ":" => Token::Colon,
        "?" => Token::QuestionMark,
        "#" => Token::Hash,
        "->" => Token::RightArrow,
        "," => Token::Comma,
        "." => Token::Dot,
        "::" => Token::Scope,
        "=" => Token::Equal,
        "//!" => Token::PackageDocComment(<Vec<&'input str>>),
        "///" => Token::DocComment(<Vec<&'input str>>),
        any => Token::AnyKeyword,
        float => Token::FloatKeyword,
        double => Token::DoubleKeyword,
        u32 => Token::Unsigned32,
        u64 => Token::Unsigned64,
        i32 => Token::Signed32,
        i64 => Token::Signed64,
        boolean => Token::BooleanKeyword,
        string => Token::StringKeyword,
        datetime => Token::DateTimeKeyword,
        bytes => Token::BytesKeyword,
        true => Token::TrueKeyword,
        false => Token::FalseKeyword,
        stream => Token::StreamKeyword,
        option => Token::OptionKeyword,
    }
}
