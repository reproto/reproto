// vim: ft=rust
use core::{RpNumber, Loc, RpModifier, RpPackage, Object};
use std::rc::Rc;
use ast::*;
use lexer::{self, Token};
use super::utils::strip_code_block;
use std::borrow::Cow;

grammar<'input, 'object>(input_object: &'object Rc<Box<Object>>);

pub File: File<'input> =
    <comment:"//!"?>
    <uses:Loc<Use>*>
    <options:Loc<OptionDecl>*>
    <decls:Decl*> => {
        File {
            comment: comment.unwrap_or_else(Vec::new),
            uses: uses,
            options: options,
            decls: decls,
        }
};

Use: UseDecl<'input> =
    "use" <package:Loc<Package>>
        <range:Loc<"string">?>
        <alias:Loc<UseAlias>?>
    ";" => {
    UseDecl {
        package: package,
        range: range,
        alias: alias,
    }
};

UseAlias: Cow<'input, str> = "as" <value:ident> => value;

Package: RpPackage = <parts:OneOrMore<".", ident>> => {
    RpPackage::new(parts.into_iter().map(|s| s.to_string()).collect())
};

Decl: Decl<'input> = {
    <en:Item<Enum>> => Decl::Enum(en),
    <interface:Item<Interface>> => Decl::Interface(interface),
    <ty:Item<Type>> => Decl::Type(ty),
    <tuple:Item<Tuple>> => Decl::Tuple(tuple),
    <service:Item<Service>> => Decl::Service(service),
};

Enum: EnumBody<'input> =
    "enum" <name:TypeIdent> <ty:("as" Loc<TypeSpec>)?> "{"
        <variants:Item<EnumVariant>*>
        <members:EnumMember*>
    "}" =>
    EnumBody {
        name: name,
        ty: ty.map(|ty| ty.1),
        variants: variants,
        members: members,
    };

EnumMember: EnumMember<'input> = {
    <option:Loc<OptionDecl>> => EnumMember::Option(option),
    <code:Loc<Code>> => EnumMember::Code(code),
    <decl:Decl> => EnumMember::InnerDecl(decl),
};

Interface: InterfaceBody<'input> =
    "interface" <name:TypeIdent> "{"
      <members:TypeMember*>
      <sub_types:Item<SubType>*>
    "}" =>
    InterfaceBody {
        name: name,
        members: members,
        sub_types: sub_types,
    };

Type: TypeBody<'input> =
    "type" <name:TypeIdent> "{"
        <members:TypeMember*>
    "}" =>
    TypeBody {
        name: name,
        members: members,
    };

Tuple: TupleBody<'input> =
    "tuple" <name:TypeIdent> "{"
        <members:TypeMember*>
    "}" =>
    TupleBody {
        name: name,
        members: members,
    };

Service: ServiceBody<'input> =
    "service" <name:TypeIdent> "{"
        <members:ServiceMember*>
    "}" =>
    ServiceBody {
        name: name,
        members: members,
    };

pub ServiceMember: ServiceMember<'input> = {
    <endpoint:Item<Endpoint>> => {
        ServiceMember::Endpoint(endpoint)
    },

    <option:Loc<OptionDecl>> => {
        ServiceMember::Option(option)
    },

    <decl:Decl> => {
        ServiceMember::InnerDecl(decl)
    },
};

Endpoint: Endpoint<'input> = {
    <id:Loc<ident>> "("
        <arguments:ZeroOrMore<",", EndpointArgument>>
    ")" <response:("->" Loc<Channel>)?>
    <alias:FieldAlias?>
    ";" => {
        Endpoint {
            id: id,
            alias: alias,
            options: vec![],
            arguments: arguments,
            response: response.map(|r| r.1),
        }
    },

    <id:Loc<ident>> "("
        <arguments:ZeroOrMore<",", EndpointArgument>>
    ")" <response:("->" Loc<Channel>)?>
    <alias:FieldAlias?> "{"
        <options:Loc<OptionDecl>*>
    "}" => {
        Endpoint {
            id: id,
            alias: alias,
            options: options,
            arguments: arguments,
            response: response.map(|r| r.1),
        }
    },
};

EndpointArgument: (Loc<Cow<'input, str>>, Loc<Channel>) =
    <name:Loc<ident>> ":" <channel:Loc<Channel>> =>
    (name, channel);

Channel: Channel = {
    <stream:stream?> <ty:TypeSpec> => {
        if stream.is_some() {
            Channel::Streaming {
                ty: ty
            }
        } else {
            Channel::Unary {
                ty: ty
            }
        }
    },
};

FieldAlias: String = {
    "as" <value:ident> => value.to_string(),
    "as" <value:"string"> => value,
};

EnumVariant: EnumVariant<'input> =
    <name:Loc<TypeIdent>> <argument:("as" Loc<Value>)?> ";" =>
    EnumVariant {
        name: name,
        argument: argument.map(|a| a.1),
    };

Modifier: RpModifier =
    <modifier:"?"?> =>
    modifier.map(|_| RpModifier::Optional).unwrap_or(RpModifier::Required);

pub TypeMember: TypeMember<'input> = {
    <field:Item<Field>> => TypeMember::Field(field),
    <option:Loc<OptionDecl>> => TypeMember::Option(option),
    <code:Loc<Code>> => TypeMember::Code(code),
    <decl:Decl> => TypeMember::InnerDecl(decl),
};

Code: Code<'input> =
    <context:ident> "{{" <content:code> "}}" =>
    Code {
        context: context,
        content: strip_code_block(content),
    };

Field: Field<'input> =
    <name:ident> <modifier:Modifier> ":" <ty:TypeSpec> <alias:FieldAlias?> ";" =>
    Field {
        modifier: modifier,
        name: name,
        ty: ty,
        field_as: alias,
    };

OptionDecl: OptionDecl<'input> =
    option <name:ident> "=" <value:Loc<Value>> ";" =>
    OptionDecl {
        name: name,
        value: value,
    };

SubType: SubType<'input> = {
    <name:Loc<TypeIdent>> <alias:("as" Loc<Value>)?> "{"
        <members:TypeMember*>
    "}" =>
    SubType {
        name: name,
        members: members,
        alias: alias.map(|alias| alias.1),
    },

    <name:Loc<TypeIdent>> <alias:("as" Loc<Value>)?> ";" =>
    SubType {
        name: name,
        members: Vec::new(),
        alias: alias.map(|alias| alias.1),
    },
};

pub Value: Value<'input> = {
    "(" <values:ZeroOrMore<",", Loc<Value>>> ")" => Value::Array(values),
    <string:"string"> => Value::String(string),
    <number:number> => Value::Number(number),
    <true> => Value::Boolean(true),
    <false> => Value::Boolean(false),
    <ident:ident> => Value::Identifier(ident),
    <ty:TypeSpec> => Value::Type(ty),
};

pub TypeSpec: Type = {
    any => Type::Any,
    float => Type::Float,
    double => Type::Double,
    i32 => Type::Signed { size: 32 },
    i64 => Type::Signed { size: 64 },
    u32 => Type::Unsigned { size: 32 },
    u64 => Type::Unsigned { size: 64 },
    boolean => Type::Boolean,
    string => Type::String,
    datetime => Type::DateTime,
    bytes => Type::Bytes,
    <name:Name> => Type::Name{
        name: name
    },
    "[" <inner:TypeSpec> "]" => Type::Array {
        inner: Box::new(inner)
    },
    "{" <key:TypeSpec> ":" <value:TypeSpec> "}" => Type::Map{
        key: Box::new(key),
        value: Box::new(value)
    },
};

Name: Name = {
    <prefix:(ident "::")?> <parts:OneOrMore<"::", TypeIdent>> => Name::Absolute {
        prefix: prefix.map(|p| p.0.to_string()),
        parts: parts.into_iter().map(|s| s.to_string()).collect(),
    },

    "::" <parts:OneOrMore<"::", TypeIdent>> => Name::Relative {
        parts: parts.into_iter().map(|s| s.to_string()).collect(),
    },
};

Attribute: Attribute<'input> = {
    "#" "[" <ident:Loc<ident>>  "]" => {
        Attribute::Word(ident)
    },

    "#" "[" <ident:Loc<ident>> "(" <name_value:ZeroOrMoreTrailing<",", AttributeItem>> ")" "]" => {
        Attribute::List(ident, name_value)
    },
};

AttributeItem: AttributeItem<'input> = {
    <ident:Loc<ident>> "=" <value:Loc<Value>> => AttributeItem::NameValue {
        name: ident,
        value: value,
    },

    <value:Loc<Value>> => {
        AttributeItem::Word(value)
    },
};

/// Zero or more matching, separated by a token.
ZeroOrMore<Sep, T>: Vec<T> =
    <value:OneOrMore<Sep, T>?> =>
    value.unwrap_or_else(|| vec![]);

/// One or more matching, separated by a token.
OneOrMore<Sep, T>: Vec<T> =
     <values:(<T> Sep)*> <last:T> => {
        let mut values = values;
        values.push(last);
        values
    };

ZeroOrMoreTrailing<Sep, T>: Vec<T> = {
     <values:(<T> Sep)*> <last:T> Sep? => {
        let mut values = values;
        values.push(last);
        values
    },
    () => vec![],
};

Loc<Inner>: Loc<Inner> = <s:@L> <inner:Inner> <e:@R> =>
    Loc::new(inner, (input_object.clone(), s, e));

Item<T>: Item<'input, T> =
    <comment:"///"?>
    <attributes:Loc<Attribute>*>
    <s:@L> <item:T> <e:@R> =>
    Item {
        comment: comment.unwrap_or_else(Vec::new),
        attributes: attributes,
        item: Loc::new(item, (input_object.clone(), s, e))
    };

extern {
    type Location = usize;
    type Error = lexer::Error;

    enum Token<'input> {
        TypeIdent => Token::TypeIdentifier(<Cow<'input, str>>),
        ident => Token::Identifier(<Cow<'input, str>>),
        number => Token::Number(<RpNumber>),
        "use" => Token::UseKeyword,
        "as" => Token::AsKeyword,
        "enum" => Token::EnumKeyword,
        "type" => Token::TypeKeyword,
        "interface" => Token::InterfaceKeyword,
        "tuple" => Token::TupleKeyword,
        "service" => Token::ServiceKeyword,
        "(" => Token::LeftParen,
        ")" => Token::RightParen,
        "{" => Token::LeftCurly,
        "}" => Token::RightCurly,
        "{{" => Token::CodeOpen,
        "}}" => Token::CodeClose,
        code => Token::CodeContent(<Cow<'input, str>>),
        "string" => Token::String(<String>),
        "[" => Token::LeftBracket,
        "]" => Token::RightBracket,
        ";" => Token::SemiColon,
        ":" => Token::Colon,
        "?" => Token::QuestionMark,
        "#" => Token::Hash,
        "->" => Token::RightArrow,
        "," => Token::Comma,
        "." => Token::Dot,
        "::" => Token::Scope,
        "=" => Token::Equal,
        "//!" => Token::PackageDocComment(<Vec<Cow<'input, str>>>),
        "///" => Token::DocComment(<Vec<Cow<'input, str>>>),
        any => Token::AnyKeyword,
        float => Token::FloatKeyword,
        double => Token::DoubleKeyword,
        u32 => Token::Unsigned32,
        u64 => Token::Unsigned64,
        i32 => Token::Signed32,
        i64 => Token::Signed64,
        boolean => Token::BooleanKeyword,
        string => Token::StringKeyword,
        datetime => Token::DateTimeKeyword,
        bytes => Token::BytesKeyword,
        true => Token::TrueKeyword,
        false => Token::FalseKeyword,
        stream => Token::StreamKeyword,
        option => Token::OptionKeyword,
    }
}
